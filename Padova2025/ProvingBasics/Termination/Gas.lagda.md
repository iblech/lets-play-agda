```
{-# OPTIONS --cubical-compatible #-}
module Padova2025.ProvingBasics.Termination.Gas where
```

# Gas

```
open import Padova2025.ProgrammingBasics.Naturals.Base
open import Padova2025.ProgrammingBasics.Naturals.Arithmetic
open import Padova2025.ProgrammingBasics.Lists
open import Padova2025.ProvingBasics.Equality.Base
```

To be convinced that a recursion eventually terminates, Agda's termination
checker needs to observe that the recursive call is on *structurally smaller*
arguments, such as `x` being structurally smaller than `succ x`. Custom
ordering relations, no matter how relevant to the situation at hand, are not
consulted by Agda's termination checker.

To satisfy the termination checker, we can introduce an additional
argument whose only purpose is to get structurally smaller at each step.

```
digits‚ÇÉ : ‚Ñï ‚Üí ‚Ñï
digits‚ÇÉ x = go (succ x) x
  where
  go : (gas : ‚Ñï) ‚Üí (x : ‚Ñï) ‚Üí ‚Ñï
  go zero       x          = zero  -- bailout, give up, should never be reached
  go (succ gas) zero       = zero
  go (succ gas) x@(succ _) = succ (go gas (half x))
```

This definition works because an initial gas of `succ x` is always sufficient to
compute the correct answer. (Using `x` as initial gas would also work, but only
because we put `zero` in the bailout case.)

However, the proof that `digits‚ÇÉ x ‚â° succ (digits‚ÇÉ (half x))` for positive `x`
is quite involved.

```code
digits‚ÇÉ-eq : (x : ‚Ñï) ‚Üí digits‚ÇÉ (succ x) ‚â° succ (digits‚ÇÉ (half (succ x)))
digits‚ÇÉ-eq x = {!!}
```

A much better way is provided by the [more sophisticated kind of
gas](Padova2025.ProvingBasics.Termination.WellFounded.html) discussed in
the next module.


## Tagging the exceptional case

We were able to imagine some (arbitrary) definition in the bailout case above,
because the codomain `‚Ñï` is inhabited. In other cases, for instance when the
codomain is some type of witnesses, no suitable "error value" might be at hand.
To make the gas method work, we then use a more explicit mechanism for
signaling the bailout case.

```
data Maybe (A : Set) : Set where
  nothing : Maybe A
  just    : A ‚Üí Maybe A
```

```
digits‚ÇÉ' : ‚Ñï ‚Üí Maybe ‚Ñï
digits‚ÇÉ' x = go (succ x) x
  where
  go : (gas : ‚Ñï) ‚Üí (x : ‚Ñï) ‚Üí Maybe ‚Ñï
  go zero       x          = nothing  -- bailout
  go (succ gas) zero       = just zero
  go (succ gas) x@(succ _) with go gas (half x)
  ... | nothing = nothing
  ... | just y  = just (succ y)
```

In this version, the original simple structure of the recursive call (`succ
(digits (half x))`) has been lost; we can try to recover it by using the
`_>>=_` operator.

```
infixl 1 _>>=_
_>>=_ : {A B : Set} ‚Üí Maybe A ‚Üí (A ‚Üí Maybe B) ‚Üí Maybe B
nothing >>= f = nothing
just x  >>= f = f x
```

```
digits‚ÇÉ'' : ‚Ñï ‚Üí Maybe ‚Ñï
digits‚ÇÉ'' x = go (succ x) x
  where
  go : (gas : ‚Ñï) ‚Üí (x : ‚Ñï) ‚Üí Maybe ‚Ñï
  go zero       x          = nothing  -- bailout
  go (succ gas) zero       = just zero
  go (succ gas) x@(succ _) = go gas (half x) >>= Œª y ‚Üí just (succ y)
```

Employing so-called [do notation](https://agda.readthedocs.io/en/stable/language/syntactic-sugar.html#do-notation),
we can improve the presentation a bit more:

```
digits‚ÇÉ''' : ‚Ñï ‚Üí Maybe ‚Ñï
digits‚ÇÉ''' x = go (succ x) x
  where
  go : (gas : ‚Ñï) ‚Üí (x : ‚Ñï) ‚Üí Maybe ‚Ñï
  go zero       x          = nothing  -- bailout
  go (succ gas) zero       = just zero
  go (succ gas) x@(succ _) = do
    y ‚Üê go gas (half x)
    just (succ y)
```

However, proofs involving any of these versions of `digits‚ÇÉ` are still quite
intricate, and also not particularly insightful. As an exercise, you could try
proving that `digits‚ÇÉ' x` is of the form `just _` for every number `x`.


### Other applications of `Maybe`

The expression `lookupMaybe xs n` should reduce to (`just` of) element
nr. `n` of the list `xs` if such an element exists, and to `nothing`
otherwise.

```
lookupMaybe : {X : Set} ‚Üí List X ‚Üí ‚Ñï ‚Üí Maybe X
-- Holify
lookupMaybe []        n        = nothing
lookupMaybe (x ‚à∑ xs) zero     = just x
lookupMaybe (x ‚à∑ xs) (succ n) = lookupMaybe xs n

-- Tests
-- EX: lookupMaybe (four ‚à∑ two ‚à∑ one ‚à∑ []) one   ‚â° just two
-- EX: lookupMaybe (four ‚à∑ two ‚à∑ one ‚à∑ []) two   ‚â° just one
-- EX: lookupMaybe (four ‚à∑ two ‚à∑ one ‚à∑ []) three ‚â° nothing
```


## Prooflessly extracting results in concrete instances

Without having to prove that the results of `digits‚ÇÉ'` are always of the form
`just _`, we can still extract the results in concrete instances. For instance,
to obtain `digits‚ÇÉ' five` as an actual element of `‚Ñï` (instead of `Maybe ‚Ñï`),
we can carry out the following preparations...

```
record ùüô : Set where
  constructor tt

module _ {A : Set} where
  From-just : Maybe A ‚Üí Set
  From-just nothing  = ùüô
  From-just (just _) = A

  from-just : (m : Maybe A) ‚Üí From-just m
  from-just nothing  = tt
  from-just (just x) = x
```

...and then obtain the value as follows.

```
example-digits-computation : ‚Ñï
-- Holify
example-digits-computation = from-just (digits‚ÇÉ' five)
-- 1. Put `from-just (digits‚ÇÉ' five)` in the hole.
-- 2. Observe, using `C-c C-v`, that `example-digits-computation` is `three`.

-- Tests
-- EX: example-digits-computation ‚â° three
```


## Exercise: Division and modulo

```
open import Padova2025.ProgrammingBasics.Booleans
open import Padova2025.ProvingBasics.Connectives.Disjunction
open import Padova2025.ProvingBasics.Termination.Ordering
```

The following snippet of code is intended to implement division, but
fails to satisfy the termination checker (and indeed, does not
terminate in case the divisor is zero).

```code
infix 7 _/_
_/_ : ‚Ñï ‚Üí ‚Ñï ‚Üí ‚Ñï
a / b with ‚â§-<-connex b a
... | left  b‚â§a = succ ((a ‚à∏ b) / b)
... | right a<b = zero
```

Use the brittle gas technique introduced in this module to provide a
terminating and correct version of division. You do not need to verify
that your implementation is indeed correct. In case the divisor is zero,
your function may compute whatever you like.

```
infix 7 _/_
_/_ : ‚Ñï ‚Üí ‚Ñï ‚Üí ‚Ñï
-- Holify
_/_ a b = go a a
  where
  go : (gas : ‚Ñï) ‚Üí (x : ‚Ñï) ‚Üí ‚Ñï
  go zero       x = zero  -- bailout
  go (succ gas) x with ‚â§-<-connex b x
  ... | left  b‚â§x = succ (go gas (x ‚à∏ b))
  ... | right x<b = zero

-- Tests
-- EX: two   / one   ‚â° two
-- EX: two   / two   ‚â° one
-- EX: two   / three ‚â° zero
-- EX: two   / four  ‚â° zero
-- EX: two   / five  ‚â° zero
-- EX: three / one   ‚â° three
-- EX: three / two   ‚â° one
-- EX: three / three ‚â° one
-- EX: three / four  ‚â° zero
-- EX: three / five  ‚â° zero
-- EX: four  / one   ‚â° four
-- EX: four  / two   ‚â° two
-- EX: four  / three ‚â° one
-- EX: four  / four  ‚â° one
-- EX: four  / five  ‚â° zero
-- EX: five  / one   ‚â° five
-- EX: five  / two   ‚â° two
-- EX: five  / three ‚â° one
-- EX: five  / four  ‚â° one
-- EX: five  / five  ‚â° one
```

Similarly, the following code is supposed to implement the modulo
operation, but fails the termination checker; provide a terminating
version.

```code
infix 7 _%_
_%_ : ‚Ñï ‚Üí ‚Ñï ‚Üí ‚Ñï
a % b with ‚â§-<-connex b a
... | left  b‚â§a = (a ‚à∏ b) % b
... | right a<b = a
```

```
infix 7 _%_
_%_ : ‚Ñï ‚Üí ‚Ñï ‚Üí ‚Ñï
-- Holify
_%_ a b = go a a
  where
  go : (gas : ‚Ñï) ‚Üí (x : ‚Ñï) ‚Üí ‚Ñï
  go zero       x = zero  -- bailout
  go (succ gas) x with ‚â§-<-connex b x
  ... | left  b‚â§x = go gas (x ‚à∏ b)
  ... | right x<b = x

-- Tests
-- EX: two   % one   ‚â° zero
-- EX: two   % two   ‚â° zero
-- EX: two   % three ‚â° two
-- EX: two   % four  ‚â° two
-- EX: two   % five  ‚â° two
-- EX: three % one   ‚â° zero
-- EX: three % two   ‚â° one
-- EX: three % three ‚â° zero
-- EX: three % four  ‚â° three
-- EX: three % five  ‚â° three
-- EX: four  % one   ‚â° zero
-- EX: four  % two   ‚â° zero
-- EX: four  % three ‚â° one
-- EX: four  % four  ‚â° zero
-- EX: four  % five  ‚â° four
-- EX: five  % one   ‚â° zero
-- EX: five  % two   ‚â° one
-- EX: five  % three ‚â° two
-- EX: five  % four  ‚â° one
-- EX: five  % five  ‚â° zero
```
